plugins {
    id 'net.neoforged.gradle.userdev' version "${neogradle_version}"
    id 'net.neoforged.gradle.mixin' version "${neogradle_mixin_version}"
    id 'com.github.johnrengelman.shadow' version "${shadow_version}"
    // Plugin to use the settings.xml for authentication
    id 'net.linguica.maven-settings' version "${maven_settings_version}"
}

apply plugin: 'maven-publish'
apply from: "https://raw.githubusercontent.com/henkelmax/mod-gradle-scripts/${mod_gradle_script_version}/mod.gradle"

jar {
    manifest {
        attributes(['Automatic-Module-Name'   : 'de.maxhenkel.corelib'])
    }
}

processResources {
    filesMatching('**/*.toml') {
        expand 'mod_version': mod_version,
                'neoforge_dependency': neoforge_dependency,
                'minecraft_version': minecraft_version
    }
}

tasks.register('apiJar', Jar) {
    archiveClassifier = 'api'
    from sourceSets.main.output
    exclude 'de/maxhenkel/corelib/dev'
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.matching(it -> it.name.startsWith("publish")).configureEach {
    it.dependsOn shadowJar
}

javadoc {
    failOnError = false
}

artifacts {
    archives jar
    archives apiJar
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact apiJar
            artifact javadocJar
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            name = 'henkelmax.public'
            url = 'https://maven.maxhenkel.de/repository/public'
        }
    }
}
